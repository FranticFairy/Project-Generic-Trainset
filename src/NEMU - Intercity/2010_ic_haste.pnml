spriteset(spriteset_2010_ic_haste_f,   "gfx/NEMU - Intercity/2010_ic_haste.png") {template_32(7, 39)}
spriteset(spriteset_2010_ic_haste_r,   "gfx/NEMU - Intercity/2010_ic_haste.png") {template_32_r(7, 64)}
spriteset(spriteset_2010_ic_haste_c,    "gfx/NEMU - Intercity/2010_ic_haste.png") {template_32(7, 89)}
spriteset(spriteset_2010_ic_haste_s,    "gfx/NEMU - Intercity/2010_ic_haste.png") {template_32(185, 39)}

// C O D E


switch(FEAT_TRAINS, SELF, sw_2010_ic_haste_rearcheck, position_in_vehid_chain_from_end){
	0 : spriteset_2010_ic_haste_r;
	return spriteset_2010_ic_haste_c;
}

switch(FEAT_TRAINS, SELF, sw_2010_ic_haste_frontcheck, position_in_vehid_chain){
	0 : spriteset_2010_ic_haste_f;
	return sw_2010_ic_haste_rearcheck;
}

switch(FEAT_TRAINS, SELF, sw_2010_ic_haste_nextcheck2, position_in_consist_from_end){
	0 : spriteset_2010_ic_haste_s;
	return spriteset_2010_ic_haste_f;
}

switch(FEAT_TRAINS, SELF, sw_2010_ic_haste_nextcheck, position_in_vehid_chain_from_end){
	0 : sw_2010_ic_haste_nextcheck2;
	return spriteset_2010_ic_haste_s;
}

switch(FEAT_TRAINS, PARENT, sw_2010_ic_haste_lencheck, num_vehs_in_consist){
	1 : spriteset_2010_ic_haste_s;
	return sw_2010_ic_haste_nextcheck;
}

switch(FEAT_TRAINS, SELF, sw_2010_ic_haste_attach, vehicle_type_id){
	item_2010_ic_haste	: return CB_RESULT_ATTACH_ALLOW  ;
	item_pgt_mu_coach	: return CB_RESULT_ATTACH_ALLOW  ;
	return string(STR_NO_COUPLE_MU_SAME) ;
}

/* Define the actual train */
item(FEAT_TRAINS, item_2010_ic_haste) {
    /* Define properties first, make sure to set all of them */
    property {
        reliability_decay:            20;
		refittable_cargo_classes:	  CC_PASSENGERS ;
		cargo_allow_refit:			  [PASS, TOUR] ;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
		misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC);
        track_type:                   RAIL;
        ai_special_flag:              AI_FLAG_PASSENGER;
        running_cost_base:            RUNNING_COST_DIESEL;
		length:						  8;
		
        refit_cost:                   0;
        // callback flags are not set manually
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:        0 kW;
        air_drag_coefficient:         0.06;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_DIESEL ;
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
        name:                         string(str_name_2010_ic_haste);
        climates_available:           ALL_CLIMATES; 
        introduction_date:            date(2010, 1, 1);
        model_life:                   40; //How long should this train be buyable
        vehicle_life:                 40; //How long should this train last before renewal
        tractive_effort_coefficient:  0.052;
        weight:                       57 ton;
        power:                        860 hp;
		cargo_capacity:				  80 ;
        speed:                        160 km/h;
		
		cost_factor:                    1;
		running_cost_factor:            1;
        loading_speed:                	5; 
    }
    /* Define graphics and callbacks
     * Setting all callbacks is not needed, only define what is used */
    graphics {
        default				:	sw_2010_ic_haste_lencheck;
		can_attach_wagon	:	sw_2010_ic_haste_attach ;
		create_effect		:	vfx_diesel_default ;
		additional_text		: 	string(STR_DESC_INTERCITY);
    }
	livery_override (item_pgt_mu_coach){
		default                     : sw_2010_ic_haste_rearcheck ;
		cargo_capacity              : 80 ;
		weight                      : 57 ;
		power                       : 860 ;
		length						: 8;
	}
}