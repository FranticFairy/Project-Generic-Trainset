// SDD BANKO - 2011

spriteset(spriteset_sdd2011_banko_fp, "gfx/EMU/Suburban/2011_SDD_Banko.png") {template_32(7, 39)}
spriteset(spriteset_sdd2011_banko_rp, "gfx/EMU/Suburban/2011_SDD_Banko.png") {template_32(7, 64)}
spriteset(spriteset_sdd2011_banko_c,  "gfx/EMU/Suburban/2011_SDD_Banko.png") {template_32(7, 89)}
spriteset(spriteset_sdd2011_banko_f,  "gfx/EMU/Suburban/2011_SDD_Banko.png") {template_32(185, 39)}
spriteset(spriteset_sdd2011_banko_r,  "gfx/EMU/Suburban/2011_SDD_Banko.png") {template_32(185, 64)}
spriteset(spriteset_sdd2011_banko_cp, "gfx/EMU/Suburban/2011_SDD_Banko.png") {template_32(185, 89)}

// C O D E

switch(FEAT_TRAINS, SELF, sw_sdd2011_banko_pantocheck_at_2, position_in_vehid_chain % 3){
	2 : spriteset_sdd2011_banko_cp;
	return spriteset_sdd2011_banko_c;
}
switch(FEAT_TRAINS, SELF, sw_sdd2011_banko_pantocheck_at_2r, position_in_vehid_chain_from_end % 3){
	2 : spriteset_sdd2011_banko_cp;
	return spriteset_sdd2011_banko_c;
}

switch(FEAT_TRAINS, SELF, sw_sdd2011_banko_pantocheck_at_1, position_in_vehid_chain % 2){
	1 : spriteset_sdd2011_banko_cp;
	return spriteset_sdd2011_banko_c;
}
switch(FEAT_TRAINS, SELF, sw_sdd2011_banko_pantocheck_at_1r, position_in_vehid_chain_from_end % 2){
	1 : spriteset_sdd2011_banko_cp;
	return spriteset_sdd2011_banko_c;
}

switch(FEAT_TRAINS, SELF, sw_sdd2011_banko_sidecheck_other, num_vehs_in_vehid_chain/2 > position_in_vehid_chain ) {
	1 : sw_sdd2011_banko_pantocheck_at_1;
	return sw_sdd2011_banko_pantocheck_at_1r;
}

switch(FEAT_TRAINS, SELF, sw_sdd2011_banko_sidecheck_4, num_vehs_in_vehid_chain/2 > position_in_vehid_chain ) {
	1 : sw_sdd2011_banko_pantocheck_at_1;
	return spriteset_sdd2011_banko_c;
}

switch(FEAT_TRAINS, SELF, sw_sdd2011_banko_sidecheck_6, num_vehs_in_vehid_chain/2 > position_in_vehid_chain ) {
	1 : sw_sdd2011_banko_pantocheck_at_2;
	return sw_sdd2011_banko_pantocheck_at_2r;
}

switch(FEAT_TRAINS, SELF, sw_sdd2011_banko_lencheck, num_vehs_in_vehid_chain ) {
	0 .. 2 : spriteset_sdd2011_banko_c;
	3 : sw_sdd2011_banko_pantocheck_at_1;
	4 : sw_sdd2011_banko_sidecheck_4;
	5 : sw_sdd2011_banko_pantocheck_at_2;
	6 .. 7 : sw_sdd2011_banko_sidecheck_6;
	return sw_sdd2011_banko_sidecheck_other;
}

switch(FEAT_TRAINS, SELF, sw_sdd2011_banko_rearcheck, [STORE_TEMP(-1, 0x10F), var[0x61, 0, 0x0000FFFF, 0xC6]]){
	item_sdd2011_banko: spriteset_sdd2011_banko_r;
	return spriteset_sdd2011_banko_rp;
}

switch(FEAT_TRAINS, SELF, sw_sdd2011_banko_frontcheck, position_in_vehid_chain){
	0 : spriteset_sdd2011_banko_fp;
	return sw_sdd2011_banko_rearcheck;
}

switch(FEAT_TRAINS, SELF, sw_sdd2011_banko_attach, vehicle_type_id){
	item_sdd2011_banko	: return CB_RESULT_ATTACH_ALLOW  ;
	item_pgt_mu_coach	: return CB_RESULT_ATTACH_ALLOW  ;
	return string(STR_NO_COUPLE_MU_SAME) ;
}

/* Define the actual train */
item(FEAT_TRAINS, item_sdd2011_banko) {
    /* Define properties first, make sure to set all of them */
    property {
        name:                         string(STR_NAME_MU_SDD_2011);
        // not available in toyland:
        climates_available:           ALL_CLIMATES; 
        introduction_date:            date(2011, 1, 1);
        model_life:                   40; //How long should this train be buyable
        vehicle_life:                 40; //How long should this train last before renewal
        reliability_decay:            20;
		refittable_cargo_classes:	  CC_PASSENGERS ;
		cargo_allow_refit:			  [PASS, TOUR] ;
		cargo_capacity:				  150 ;
        speed:                        130 km/h;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
		misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC);
        track_type:                   ELRL;
        ai_special_flag:              AI_FLAG_PASSENGER;
        power:                        700 hp;
        running_cost_base:            RUNNING_COST_ELECTRIC;
        weight:                       45 ton;
		length:						  8;
		dual_headed:				  1;
        tractive_effort_coefficient:  0.28;
		
        loading_speed:                6; 
        cost_factor:                  11;
        running_cost_factor:          71;
        refit_cost:                   0;
        // callback flags are not set manually
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:        0 kW;
        air_drag_coefficient:         0.06;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_ELECTRIC ;
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
    }
    /* Define graphics and callbacks
     * Setting all callbacks is not needed, only define what is used */
    graphics {
        default				:	sw_sdd2011_banko_frontcheck;
		can_attach_wagon	:	sw_sdd2011_banko_attach ;
		create_effect		:	vfx_ohle ;
    }
	livery_override (item_pgt_mu_coach){
		default                     : sw_sdd2011_banko_lencheck ;
		cargo_capacity              : 150 ;
		weight                      : 45 ;
		power                       : 350 ;
		length						: 8;
	}
}